buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['AliyunReactNativePush_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["AliyunReactNativePush_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.aliyun.ams.push"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
  maven { url 'https://maven.aliyun.com/nexus/content/repositories/releases/' }
  maven { url 'https://developer.huawei.com/repo/' }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation "com.aliyun.ams:alicloud-android-push:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-meizu:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-vivo:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-oppo:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-xiaomi:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-huawei:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-honor:[3.9.4.1,4.0)"
  implementation "com.aliyun.ams:alicloud-android-third-push-fcm:[3.9.4.1,4.0)"
}

react {
  jsRootDir = file("../src/")
  libraryName = "AliyunReactNativePush"
  codegenJavaPackageName = "com.aliyun.ams.push"
}
